group 'com.mgz'
version '0.2'

apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'eclipse'

check.dependsOn jacocoTestReport

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
}

javadoc {
    options.encoding = 'UTF-8'
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

clean.doFirst {
    delete "${rootDir}/output/"
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

configurations {
    checkstyleConfig
}

dependencies {
    // Use Checkstyle 6.19 because this is the latest version that is compatible with
    // Java 1.7. Since we set sourceCompatibility to 1.7 we should also allow a developer
    // to use Java 1.7 to build and check.....
    checkstyleConfig ("com.puppycrawl.tools:checkstyle:6.19") {
        transitive = false
    }
}

checkstyle {
    toolVersion = '6.19'
    config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
}

jacocoTestReport {
  reports {
    xml.enabled = true
    html.enabled = true
  }
}
